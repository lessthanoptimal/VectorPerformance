apply plugin: 'java-library'

group 'lessthanoptimal'
version '0.1'

java {
    withJavadocJar()
    withSourcesJar()
    toolchain { languageVersion = JavaLanguageVersion.of(16) }
}

javadoc { configure(options) { enabled=false } }
test { useJUnitPlatform() }

tasks.withType(AbstractCompile) {
    options.compilerArgs += ["--enable-preview","--add-modules", "jdk.incubator.vector"]
}

tasks.withType(Test) {
    jvmArgs += ["--enable-preview","--add-modules", "jdk.incubator.vector"]
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = 'https://jitpack.io' } // Allows annotations past Java 8 to be used
}

dependencies {
    ['1.27'].each { String a ->
        api('org.openjdk.jmh:jmh-core:' + a)
        annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:' + a
    }

    ['ejml-core','ejml-ddense'].each { String a ->
        api group: 'org.ejml', name: a, version: '0.40'}

    testImplementation( 'org.junit.jupiter:junit-jupiter-api:5.4.0')
    testRuntimeOnly( 'org.junit.jupiter:junit-jupiter-engine:5.4.0')
}

task runtimeBenchmark(type: JavaExec) {
    dependsOn build
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(16)
    }
    jvmArgs += ["--enable-preview","--add-modules", "jdk.incubator.vector"]
    group = "Execution"
    description = "Runs the benchmark code"
    classpath = sourceSets.main.runtimeClasspath
    main = "benchmark.BenchmarkOperations"
}


wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '7.0-milestone-3'
}
